plugins {
    id 'java-library'
    id 'idea'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
}

group = 'com.github.wenhao'
version = '1.0.2'

repositories {
    jcenter()
}

def compatibility = {
    sourceCompatibility=1.8
    targetCompatibility=1.8
}
compileJava(compatibility)
compileTestJava(compatibility)

dependencies {
    testCompile "junit:junit:$junitVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    scm {
        url 'https://github.com/wenhao/geohash'
        connection 'https://github.com/wenhao/geohash.git'
        developerConnection 'git@github.com:wenhao/geohash.git'
    }

    developers {
        developer {
            id 'wenhao'
            name 'Hao Wen'
            email 'wenhao@126.com'
            organization 'Hao Wen'
            roles {
                role 'Developer'
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.wenhao'
            artifactId 'geohash'
            version '1.0.2'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', '52 bit GeoHash search algorithm.')
                root.appendNode('name', 'mushrooms')
                root.appendNode('url', 'https://github.com/wenhao/geohash')
                root.appendNode('inceptionYear', '2016')
                root.children().last() + pomConfig
            }
        }
    }
}

// publish to jcenter
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['maven']
    dryRun = false
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = 'geohash'
        desc = '52 bit GeoHash search algorithm.'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/wenhao/geohash'
        issueTrackerUrl = 'https://github.com/wenhao/geohash/issues'
        vcsUrl = 'https://github.com/wenhao/geohash'
        labels = ['geo', 'geohash']
        publicDownloadNumbers = true
        githubRepo = 'wenhao/geohash'
        githubReleaseNotesFile = 'README.md'
        version {
            name = '1.0.2'
            desc = '52 bit GeoHash search algorithm.'
            released = new Date()
            vcsTag = '1.0.2'
            gpg {
                sign = true
                passphrase = 'passphrase'
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUsername')
                password = project.hasProperty('sonatypePassword')
                close = '1'
            }
        }
    }
}